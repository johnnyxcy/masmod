;; 1. Based on: 3
;; 2. Description: PK-PD ce
$PROBLEM    warfarin PK-PD model
$INPUT      ID TIME AMT DV DVID MDV WT AGE SEX
$DATA      warfarin.csv IGNORE=#
$SUBROUTINE ADVAN13 TOL=9
$MODEL      COMP(GUT,DEFDOS) COMP(CENTRAL,DEFOBS) COMP(EFFECT)
$PK
   ; PK
   CL = THETA(1) * (WT/70)**0.75 * EXP(ETA(1))
   V2 = THETA(2) * (WT/70) * EXP(ETA(2))
   KA = THETA(3) * EXP(ETA(3))
   ALAG1 = THETA(4) * EXP(ETA(4))

   S2 = V2
   K20 = CL / V2

   ; PD 
   E0 = THETA(5) * EXP(ETA(5))
   EMAX = THETA(6) * EXP(ETA(6))
   EC50 = THETA(7) * EXP(ETA(7))
   KE0 = THETA(8) * EXP(ETA(8))

$DES
   DADT(1) = -KA * A(1)
   DADT(2) =  KA * A(1) - K20 * A(2)
   DADT(3) =  KE0 * (A(2)/V2 - A(3))

$ERROR
   CP = A(2) / S2
   CE = A(3)
   
   EFF = E0 + (EMAX * CE) / (EC50 + CE)
   IF (DVID.LE.1) THEN
      IPRED = CP
      Y = CP * (1 + ERR(1)) + ERR(2)
   ELSE
      IPRED = EFF
      Y = EFF + ERR(3)
   ENDIF

$THETA  (0.01,0.131,1) FIX ; [CL] L/h/70kg
 (0.01,8.06,20) FIX ; [V] L/70kg
 (0.01,1.325) FIX ; [KA] 1/h
 (0.01,1.03,24) FIX ; [ALAG1] h
$THETA  (0.01,96.7216,200) ; [E0]
 (-500,-254.891,0) ; [EMAX]
 (0.01,11.0351,20) FIX ; [EC50]
 (0.001,0.0175966,3) ; [KE0]
$OMEGA  0.0608416  ;   ETA1[CL]
 0.0193153  ;    ETA2[V]
 0.452  FIX  ;   ETA3[KA]
 0.303  FIX  ; ETA4[ALAG1]
$OMEGA  0.00264168  ;   ETA5[E0]
 0  FIX  ; ETA6[EMAX]
 0.0364006  FIX  ; ETA7[EC50]
 0.0589568  ;  ETA8[KE0]
$SIGMA  0.00169157  ; EPS1[PROP PK]
 0.646078  ; EPS2[ADD PK]
 14.3981  ; EPS3[ADD PD]
$ESTIMATION METHOD=COND INTER MAXEVAL=9999 PRINT=1 NOABORT
$COVARIANCE
$TABLE      ID TIME DV MDV DVID PRED IPRED CWRES CIWRES ONEHEADER
            NOPRINT NOAPPEND FILE=SDTAB5
$TABLE      ID ETA(1) ETA(2) ETA(4) ETA(7) ETA(8) NOPRINT NOAPPEND
            ONEHEADER FILE=PATAB5
$TABLE      ID SEX NOPRINT NOAPPEND ONEHEADER FILE=CATAB5
$TABLE      ID WT AGE NOPRINT NOAPPEND ONEHEADER FILE=COTAB5
;; $PLOT INDIVIDUAL XAXIS=TIME BY=DVID

;; $PLOT GOF XAXIS=TIME BY=DVID

;; $PLOT CORR

